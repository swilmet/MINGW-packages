# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=gtksourceview
pkgbase=mingw-w64-${_realname}4
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}4"
pkgver=4.0.40
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME (mingw-w64)"
arch=('any')
url="https://github.com/gedit-org"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "autoconf-archive")
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-libxml2")
source=("https://github.com/gedit-org/gtksourceview/releases/download/${pkgver}/gtksourceview-${pkgver}.tar.xz")
sha256sums=('c565c830b38fd427f72bf6a404f9bd4b63669a3536af119e49fa616174d3f17a')

prepare() {
  cd ${_realname}-${pkgver}
  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --disable-static \
    --disable-glade-catalog \
    --disable-gtk-doc

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}4/COPYING"
}
